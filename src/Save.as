string GenerateCSharpClass() {
    string classContent = GenerateMetadata();
    classContent += "class " + general.className + ": Alteration {\n";
    classContent += "    public override void run(Map map){\n";

    for (uint i = 0; i < components.Length; i++) {
        classContent += GenerateMethodContent(components[i]);
    }

    classContent += "        map.placeStagedBlocks();\n";
    classContent += "    }\n";
    classContent += "}\n";

    return classContent;
}

string GenerateMethodContent(const ComponentInfo &in component) {
    string typeStr = BlockTypeToString(component.componentType);
    switch (component.methodType) {
        case MethodType::REPLACE:
            return GenerateReplace(component.componentInputArray, component.componentOutput, typeStr);
        case MethodType::DELETE:
            return GenerateDelete(component.componentInputArray);
        case MethodType::PLACE:
            return GeneratePlace(component.componentInputArray[0], component.componentOutput, typeStr);
        case MethodType::PLACERELATIVE:
            return GeneratePlaceRelative(component.componentInputArray, component.componentOutput, typeStr);
        default:
            return "";
    }
}

string BlockTypeToString(ComponentType type) {
    switch (type) {
        case ComponentType::AUTO:
            return "Auto";
        case ComponentType::BLOCK:
            return "Block";
        case ComponentType::ITEM:
            return "Item";
        case ComponentType::CUSTOM:
            return "Custom";
        default:
            return "Unknown";
    }
}

string GenerateMetadata() {
    return "// Auto-generated by Alteration Helper\n"
           "// Version: " + general.version + "\n"
           "// Author: " + general.userName + "\n"
           "// Creation Date: " + general.creationDate + "\n"
           "// Description: " + general.description + "\n\n";
}

string GenerateReplace(array<string> blocks, const string &in newBlock, const string &in type) {
    string output = "";
    for (uint i = 0; i < blocks.Length; i++) {
        output += "        map.replace(\"" + blocks[i] + "\", \"" + newBlock + "\", BlockType." + type + ");\n";
    }
    return output;
}

string GenerateDelete(const array<string> &blocks) {
    string output = "";
    for (uint i = 0; i < blocks.Length; i++) {
        output += "        map.delete(\"" + blocks[i] + "\");\n";
    }
    return output;
}

string GeneratePlace(const string &in block, const string &in newBlock, const string &in type) {
    return "        map.place(\"" + block + "\", \"" + newBlock + "\", BlockType." + type + ");\n";
}

string GeneratePlaceRelative(array<string> blocks, const string &in newBlock, const string &in type) { 
    string output = "";
    for (uint i = 0; i < blocks.Length; i++) {
        output += "        map.placeRelative(\"" + blocks[i] + "\", \"" + newBlock + "\", BlockType." + type + ");\n";
    }
    return output;
}
